#!/usr/bin/perl -w
#
# check_snmp_hr_processes - Check running processes using SNMP
#
# Copyright (C) 2011 Oskar Liljeblad
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
# Run check_snmp_hr_processes --help for usage information.
# Written by Oskar Liljeblad <oskar@osk.mine.nu>.
#

require 5.10.0;
use strict;
use Net::SNMP;
use Getopt::Long;
use Nagios::Plugin;

my $PROGRAM = 'check_snmp_hr_processes';
my $VERSION = '0.1';

my @state_name = ('Unknown', 'Invalid', 'Not Runnable', 'Runnable', 'Running');

my $plugin = Nagios::Plugin->new(shortname => $PROGRAM);
my %opt = (
  'community' => 'public',
  'snmp-version' => 1,
  'timeout' => 30, # 30 is the default Net::SNMP timeout
  'state' => 'running',
);
Getopt::Long::config('bundling');
Getopt::Long::GetOptions(\%opt,
  'host|H=s',
  'timeout|t=i',
  'community|C=s',
  'snmp-version|V=s',
  'state=s',
  'comma-separated',
  'list|l',
  'help',
  'version'
) || exit UNKNOWN;

if (defined $opt{'help'}) {
  print <<"__END__";
Usage: $PROGRAM -H ADDRESS [OPTION]... PROCESS...
Check running processes using HOST-RESOURCES-V2-MIB SNMP requests. Exit
with critical status if any process specified on the command line is not
running.

  -H, --host=ADDRESS       address or name of host to check
  -t, --timeout=SECONDS    total time for SNMP checks
  -C, --community=NAME     SNMP community
  -V, --snmp-version=VER   SNMP version (1, 2c, 3)
  -l, --list               list all services and their states
      --state=STATE        match state instead of default `running'
      --comma-separated    services on command line are comma-separated
      --help               display this help and exit
      --version            output version information and exit

The following states are available:

  unknown, invalid, not-runnable, runnable, running

Report $PROGRAM bugs to <oskar\@osk.mine.nu>.
__END__
  exit;
}
if (defined $opt{'version'}) {
  print "$PROGRAM $VERSION\n";
  print "Copyright (C) 2011 Oskar Liljeblad\n";
  print "License AGPLv3+: GNU Affero GPL version 3 or later <http://gnu.org/licenses/agpl.html>.\n";
  print "This is free software: you are free to change and redistribute it.\n";
  print "There is NO WARRANTY, to the extent permitted by law.\n";
  print "\nWritten by Oskar Liljeblad.\n";
  exit;
}
$plugin->nagios_exit(UNKNOWN, "missing --host/-H parameter\n") if !defined $opt{'host'};

my $want_state = parse_state_name($opt{'state'});
$plugin->nagios_exit(UNKNOWN, "unknown state `$opt{'state'}'\n") if !defined $want_state;

my @process_names;
if ($opt{'comma-separated'}) {
  push @process_names, split(/,/, $_) foreach @ARGV;
} else {
  @process_names = @ARGV;
}
$plugin->nagios_exit(UNKNOWN, "no processes to check") if !@process_names && !$opt{'list'};

my $retries = 1; # default number of SNMP retries
my ($session, $error) = Net::SNMP->session(
    -hostname => $opt{'host'},
    -community => $opt{'community'},
    -version => $opt{'snmp-version'},
    -timeout => int($opt{'timeout'}/($retries+1)));
$plugin->nagios_exit(UNKNOWN, "cannot open SNMP session: $error\n") if !defined $session;

my $baseoid = '1.3.6.1.2.1.25.4.2.1';

my $result = $session->get_table(-baseoid => $baseoid);
$plugin->nagios_exit(UNKNOWN, "cannot get processes table: ".$session->error."\n") if !defined $result;

my %processes;
foreach my $key (keys %{$result}) {
  if (substr($key, 0, length("$baseoid.2.")) eq "$baseoid.2.") {
    my $id = substr($key, length("$baseoid.2."));
    my $name = $result->{"$baseoid.2.$id"};
    $processes{$name} = $id if defined $name; # FIXME how to handle dupes?
  }
}

if ($opt{'list'}) {
  foreach my $name (sort keys %processes) {
    my $state_id = get_process_state_by_id($processes{$name});
    print "$name: $state_name[$state_id]\n";
  }
  exit;
}

my $status = OK;
my %state;
foreach my $name (@process_names) {
  my $id = $processes{$name};
  my $state_id = get_process_state_by_id($id);
  $status = CRITICAL if $state_id != $want_state;
  push @{$state{$state_id}}, defined $id ? $result->{"$baseoid.2.$id"} : $name;
}
my @results;
foreach my $state_id (sort keys %state) {
  push @results, $state_name[$state_id].': '.join(', ', @{$state{$state_id}});
}
$plugin->nagios_exit($status, join(', ', @results));

sub get_process_state_by_id {
  my ($id) = @_;
  return 0 if !defined $id; # Unknown
  my $status = $result->{"$baseoid.7.$id"};
  return 0 if !defined $status || $status !~ /^[1-4]$/; # Unknown
  return 1 if $status == 4; # Invalid
  return 2 if $status == 3; # Not Runnable
  return 3 if $status == 2; # Runnable
  return 4 if $status == 1; # Running
}

sub parse_state_name {
  my ($state) = @_;
  $state =~ s/-/ /;
  for (my $c = 0; $c < @state_name; $c++) {
    return $c if lc $state eq lc $state_name[$c];
  }
  return undef;
}
